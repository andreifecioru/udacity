apply plugin: 'com.android.application'
apply plugin: 'com.google.cloud.tools.endpoints-framework-client'

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.google.cloud.tools:endpoints-framework-gradle-plugin:1.0.2'
    }
}

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        multiDexEnabled true

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'tier'
    productFlavors {
        free {
            dimension 'tier'
            applicationId "com.udacity.gradle.builditbigger.free"
        }
        paid {
            dimension 'tier'
            applicationId "com.udacity.gradle.builditbigger.paid"
        }
    }
}

dependencies {
    def dagger_version = '2.4'

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    endpointsServer project(path: ':backend', configuration: 'endpoints')
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation "com.android.support:design:28.0.0"
    implementation 'android.arch.lifecycle:extensions:1.1.1'

    implementation "com.google.dagger:dagger:$dagger_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_version"

    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'

    implementation 'com.google.api-client:google-api-client:1.23.0'
    implementation 'com.google.http-client:google-http-client-android:1.23.0'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    // Google Ads - only for the free tier
    freeImplementation 'com.google.android.gms:play-services-ads:17.1.2'

    implementation project(":jokedisplay")

    // Testing dependencies
    implementation 'com.android.support.test.espresso:espresso-idling-resource:3.0.2'

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'com.android.support:support-annotations:28.0.0'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-intents:3.0.2'
}

task runConnectedTests() {
    description "Run instrumented test while starting/stopping the backed"
    group "Custom tasks"

    dependsOn = ["connectedAndroidTest", ":backend:appengineStart"]
    finalizedBy ":backend:appengineStop"
}

afterEvaluate {
    tasks.getByName("connectedAndroidTest").mustRunAfter(":backend:appengineStart")
}


